
namespace GUIKit {
	final string ViewState = "____VIEWSTATE____";
	final string NoValue   = "____VIEWSTATE_NO_GIVEN_VALUE____";

	class Component {
		string name;
		number weight = 0;
		array  depends;
		string implementation;
		void   klass;
		
		static function register( string name, array depends, string implementation ) {
			void klass = self;
			return new klass( name, depends, implementation );
		}
		constructor( string name, array depends, string implementation ) {
			if( not GUIKit.components.keyExists(name) ) {
				GUIKit.components[name] = self;
				.name = name;
				.depends = depends;
				.implementation = implementation;
			}
		}
		function loadCode {
			string path = .pathForComponentResource("${.name}.feh");
			debug(__FILE__,__LINE__,"Loading component code for ${.name}");
			include( path );
			.klass = Class.locate(.implementation);
			if( .klass == null ) {
				raise new Error("Unable to find implementation class '${.implementation}' for component '${.name}' in loaded code '$path'");
			}
		}
		function setClass( void klass ) 
			.klass = klass;
		function pathForComponentResource( string name )
			return __pathFor( 'Support', "${.name}/$name", 'Components' );
		function uriForComponentResource( string name )
			return __uriFor( 'Support', "${.name}/$name", 'Components' );
		
		function dependancies
			return .depends;
		function templateResources
			return [ "${.name}.template" ];
		function javascript
			return [ "${.name}.js" ];
		function css
			return [ "${.name}.css" ];
	}
	
	array components;
	
	function loadComponent( string name ) {
		object component = null;
		if( not .components.keyExists(name) ) {
			string path = __pathFor( 'Support', name + '/Core.feh', 'Components' );
			if( path ) {
				include( path );
				if( .components.keyExists(name) ) {
					number maxWeight = 0;
					component = .components[name];
					component.dependancies().each() using ( dependant ) {
						object c = GUIKit.loadComponent( dependant );
						if( c.weight > maxWeight ) 
							maxWeight = c.weight;
					};
					component.weight = maxWeight + 1;
					component.loadCode();
				}
			}
		} else {
			component = .components[name];
		}
		return component;
	}
	function componentByName( string name ) {
		if( .components.keyExists(name) ) {
			return .components[name];
		}
		return null;
	}
	function componentList {
		array list = [];
		.components.each() using ( component ) {
			number w = component.weight;
			if( not list.keyExists("$w") ) {
				list["$w"] = [];
			}
			list["$w"][] = component.name;
		};
		return list;
	}
	function requiredHeaders {
		array componentList = .components.sort() using ( a, b ) {
			if( a.weight < b.weight )
				return -1;
			if( a.weight == b.weight )
				return 0;
			if( a.weight > b.weight )
				return 1;
		};
		string list = '';
		componentList.each() using ( component ) {
			component.javascript().each() using ( js ) {
				string uri = component.uriForComponentResource(js);
				if( uri ) {
					list += HTML.javascriptHeader( uri );
				}
			};
			component.css().each() using ( css ) {
				string uri = component.uriForComponentResource(css);
				if( uri ) {
					list += HTML.stylesheetHeader( uri );
				}
			};
		};
		return list;
	}
}

// Our render engine
uses "Render/Render.feh";

// Event processing
uses "Components/Events.feh";

// Separate classes
uses "Components/Page.feh";
uses "Components/Toolbox.feh";

// Main abstract classes
uses "Components/View.feh";
uses "Components/Container.feh";

// Non-container classes
uses "Components/Hidden.feh";
uses "Components/Label.feh";
uses "Components/PlainText.feh";
uses "Components/Button.feh";
uses "Components/TextField.feh";
uses "Components/PasswordField.feh";
uses "Components/CheckBox.feh";
uses "Components/TextView.feh";
uses "Components/PopupButton.feh";
uses "Components/FileLoader.feh";
uses "Components/List.feh";

uses "Components/HTMLSnippet.feh";
uses "Components/Calendar.feh";
uses "Components/SelectBox.feh";
uses "Components/ColourPicker.feh";
uses "Components/GridbagLayout.feh";


uses "Components/AJAXLabel.feh";
uses "Components/AJAXMultipleLabel.feh";
uses "Components/AJAXPopupButton.feh";
uses "Components/AJAXProgressBar.feh";
uses "Components/AJAXList.feh";

// Container classes
uses "Components/Box.feh";
uses "Components/TabView.feh";
uses "Components/TabViewItem.feh";
uses "Components/Table.feh";
uses "Components/RadioGroup.feh";
uses "Components/Dialog.feh";

// /* GAJDO
uses "Components/RestrictedView.feh";
uses "Components/Helper.feh";

// Complex classes
uses "Components/Document.feh";
// */

// Validation classes
uses "Components/Validator.feh";
