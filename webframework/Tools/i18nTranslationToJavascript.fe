uses "/cention/etc/webframework-config.feh";
uses "webframework/webframework";
uses "posix";

function translationsForJavascript( string language, array catalog ) {
	string output = '';
	output += "// Desired language: $language\n";
	catalog.map() using ( key, translation ) {
		translation = Regexp.replaceAll("'", translation, "\\'");
		output += "TranslationDictionary['${JSProcessString(key)}'] = '$translation';\n";
	};
	return output;
}
function JSProcessString( string key ) {
	key = Regexp.replaceAll("\\\\", key, "\\\\\\");
	key = Regexp.replaceAll("'", key, "\\'"); 
	return key;
}

string outputPath = "${Config.PublicPath}Resources/Javascript/Generated";
string webroot = argv[0];
string applicationName = argv[1];
string language = argv[2];
string translationFile = "$webroot/$applicationName.app/Resources/Strings/$language/translation.strings.fe";
string javascriptFile = "$outputPath/$applicationName.translation.$language.js";
array  translation = [];

monitor { 
	translation = include(translationFile);

	Directory.build(outputPath);
	File.remove(javascriptFile);
	File.create(javascriptFile) using ( file ) {
		file.writeln(translationsForJavascript(language, translation));
	};
	Console.println("Created $javascriptFile");
} handle {
	Console.println("Unable to load translation for language $language: ${err.str}");
}

