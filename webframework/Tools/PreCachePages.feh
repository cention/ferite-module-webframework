uses "/cention/etc/webframework-config.feh";
uses "webframework/webframework";

class modifies Directory {
	static function build( string path ) {
		array list = [];
		boolean change = false;
		list = path.toArray('/');
		list.eachWithIndex() using ( element, index ) {
			if( index > 0 ) {
				string directory = list[..index].join('/');
				object directory_o = Directory.open(directory);
				if( not directory_o ) {
					Directory.make(directory, 0755);
					change = true;
				}
			}
		};
		return change;
	}
}

string outputPath = "${Config.PublicPath}Resources/Javascript/Generated";
object matchPage = new Regexp( '((.*?)/([A-Za-z_]*))\.page$' );
array  included = [];
array  componentList;

webframeworkConsoleTool = true;
include("${Config.PublicPath}/Applications/${argv[0]}.app/${argv[0]}.fe");
application.filePath = "${Config.PublicPath}/Applications/${argv[0]}.app/";
application.urlPath = "/${Config.RootOffset}/${argv[0]}.app/";

metrics = new FakeMetrics();

Console.println( "Walking directory ${Config.PublicPath}/Applications/${argv[0]}.app" );
Directory.walk("${Config.PublicPath}/Applications/${argv[0]}.app") using ( file ) {
	if( matchPage.match(file) ) {
		string page = matchPage.lastMatch().capture(0) - ("${Config.PublicPath}/Applications/${argv[0]}.app/Pages/");
		string cpath = pathForPageCode( page );
		void   __PageKlass__;
		string tmp = '';

		monitor {
			__PageKlass__ = include( cpath );
			tmp = __PageKlass__.getTemplateName();
			Cache.page = new __PageKlass__();
			currentPage = Cache.page;
			Console.println(" - Caching page '$page'");
			GUIKit.EventHandlers = [];
			Cache.PreCachePage( page, __PageKlass__.getContentType(), __PageKlass__, new __PageKlass__() );
			Cache.components.each() using ( obj ) {
				componentList[Class.name(obj.getClass())] = true;
			};
		} handle ( e ) {
			Console.println("!!! Unable to cache '$page': ${e.str}");
		}
	}
};

componentList = componentList.keys();
Console.println( componentList );

