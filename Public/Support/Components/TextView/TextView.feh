namespace modifies GUIKit {
	
	class CTextView extends GUIKit.CTextfield {
		
		[attribute number rows 5 'Number of rows to have in the text view'];
		[attribute number columns 100 'Number of columns to have in the text view'];
		[attribute boolean readOnly false 'If the text view should be read only'];
		[attribute boolean richText false 'If rich text should be enabled in the text view'];
		[attribute boolean ckeditor false 'If Ckeditor should be used as the editor'];
		[attribute string ckeditorToolbar 'Full' 'The toolbar to use in Ckeditor'];
		
		function textValue() {
			string value = .textValue;
			if( .ckeditor() and not .richText() ) {
				value = Regexp.replaceAll('<br[\s]*[/]?>', value, '--line-break--');
				value = Regexp.replaceAll("(\r\n|[\r\n])", value, '');
				value = Regexp.replaceAll('<\/?[^>]+>', value, '');
				[ 'nbsp'  => ' ', 'amp' => '&', 'lt' => '<', 'gt' => '>' ].map() using ( entity, text ) {
					value = Regexp.replaceAll('&' + entity + ';', value, text);
				};
				value = value.trim(" \r\n");
				value = Regexp.replaceAll('--line-break--', value, "\r\n");
			}
			return value;
		}
		
		function render() {
			if( .visible() ) {
				array values = .generateTemplateAttributes('wfTextView');
				object component = GUIKit.componentByName('TextView');
				string path = component.pathForComponentResource('TextView.template');
				values['JavascriptSafeTextValue'] = JSON.escape(values['TextValue']);
				values['ckeditor'] = (not .readOnly() ? (.ckeditor() or .richText() ? true : false) : false);
				values['CkeditorHeight'] = .size.height - 'px';
				return GUIKit.ViewTemplate.renderPath( path, values );
			}
			return "";
		}
	}
}
